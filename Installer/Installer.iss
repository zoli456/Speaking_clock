; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!
#include "CodeDependencies.iss"
#define MyAppName "Beszélő óra"
#define MyAppVersion "1.51"
#define MyAppPublisher "Zoli456 Software"
#define MyAppURL "https://zsoftware.ct.ws/speaking_clock.html"
#define MyAppExeName "Speaking_Clock.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{2DE96183-A1E5-408C-BBCC-CF513F65AF0C}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={autopf}\{#MyAppName}
; "ArchitecturesAllowed=x64compatible" specifies that Setup cannot run
; on anything but x64 and Windows 11 on Arm.
ArchitecturesAllowed=x64compatible
; "ArchitecturesInstallIn64BitMode=x64compatible" requests that the
; install be done in "64-bit mode" on x64 or Windows 11 on Arm,
; meaning it should use the native 64-bit Program Files directory and
; the 64-bit view of the registry.
ArchitecturesInstallIn64BitMode=x64compatible
DisableProgramGroupPage=yes
; Uncomment the following line to run in non administrative install mode (install for current user only.)
PrivilegesRequired=admin
OutputBaseFilename=Speaking_clock_setup
SetupIconFile=G:\Programok\Speaking_Clock\Speaking_Clock\download-icon-clock-131964752299044081_128.ico
Compression=lzma
SolidCompression=yes
WizardStyle=modern

[Languages]
Name: "hungarian"; MessagesFile: "compiler:Languages\Hungarian.isl"

[Components]
Name: "main"; Description: "Alkalmazás"; Types: full compact custom; Flags: fixed
Name: "vcredist"; Description: "Visual C++ Könyvtárak"; Types: full custom; Flags: checkablealone


[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "G:\Programok\Speaking_Clock\Speaking_Clock\bin\x64\Release\net9.0-windows10.0.19041.0\protected\{#MyAppExeName}"; DestDir: "{app}"; Flags: ignoreversion
Source: "G:\Programok\Speaking_Clock\Speaking_Clock\bin\x64\Release\net9.0-windows10.0.19041.0\protected\vosk-model-small-en-us-0.15\*"; DestDir: "{app}\vosk-model-small-en-us-0.15"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "G:\Programok\Speaking_Clock\Speaking_Clock\bin\x64\Release\net9.0-windows10.0.19041.0\protected\Fájlok\*"; DestDir: "{app}\Fájlok"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "G:\Programok\Speaking_Clock\Speaking_Clock\bin\x64\Release\net9.0-windows10.0.19041.0\protected\Hangok\*"; DestDir: "{app}\Hangok"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "G:\Programok\Speaking_Clock\Speaking_Clock\bin\x64\Release\net9.0-windows10.0.19041.0\protected\Assets\*"; DestDir: "{app}\Assets"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "G:\Programok\Speaking_Clock\Speaking_Clock\bin\x64\Release\net9.0-windows10.0.19041.0\protected\DllInjector\*"; DestDir: "{app}\DllInjector"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "G:\Programok\Speaking_Clock\Speaking_Clock\bin\x64\Release\net9.0-windows10.0.19041.0\protected\bass.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "G:\Programok\Speaking_Clock\Speaking_Clock\bin\x64\Release\net9.0-windows10.0.19041.0\protected\bassmix.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "G:\Programok\Speaking_Clock\Speaking_Clock\bin\x64\Release\net9.0-windows10.0.19041.0\protected\HtmlAgilityPack.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "G:\Programok\Speaking_Clock\Speaking_Clock\bin\x64\Release\net9.0-windows10.0.19041.0\protected\ManagedBass.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "G:\Programok\Speaking_Clock\Speaking_Clock\bin\x64\Release\net9.0-windows10.0.19041.0\protected\ManagedBass.Mix.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "G:\Programok\Speaking_Clock\Speaking_Clock\bin\x64\Release\net9.0-windows10.0.19041.0\protected\Microsoft.Win32.TaskScheduler.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "G:\Programok\Speaking_Clock\Speaking_Clock\bin\x64\Release\net9.0-windows10.0.19041.0\protected\settings.ini"; DestDir: "{app}"; Flags: ignoreversion
Source: "G:\Programok\Speaking_Clock\Speaking_Clock\bin\x64\Release\net9.0-windows10.0.19041.0\protected\SharpCompress.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "G:\Programok\Speaking_Clock\Speaking_Clock\bin\x64\Release\net9.0-windows10.0.19041.0\protected\SharpConfig.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "G:\Programok\Speaking_Clock\Speaking_Clock\bin\x64\Release\net9.0-windows10.0.19041.0\protected\SharpGen.Runtime.COM.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "G:\Programok\Speaking_Clock\Speaking_Clock\bin\x64\Release\net9.0-windows10.0.19041.0\protected\SharpGen.Runtime.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "G:\Programok\Speaking_Clock\Speaking_Clock\bin\x64\Release\net9.0-windows10.0.19041.0\protected\Speaking_clock.deps.json"; DestDir: "{app}"; Flags: ignoreversion
Source: "G:\Programok\Speaking_Clock\Speaking_Clock\bin\x64\Release\net9.0-windows10.0.19041.0\protected\Speaking_clock.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "G:\Programok\Speaking_Clock\Speaking_Clock\bin\x64\Release\net9.0-windows10.0.19041.0\protected\Speaking_clock.runtimeconfig.json"; DestDir: "{app}"; Flags: ignoreversion
Source: "G:\Programok\Speaking_Clock\Speaking_Clock\bin\x64\Release\net9.0-windows10.0.19041.0\protected\Telerik.WinControls.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "G:\Programok\Speaking_Clock\Speaking_Clock\bin\x64\Release\net9.0-windows10.0.19041.0\protected\Telerik.WinControls.UI.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "G:\Programok\Speaking_Clock\Speaking_Clock\bin\x64\Release\net9.0-windows10.0.19041.0\protected\TelerikCommon.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "G:\Programok\Speaking_Clock\Speaking_Clock\bin\x64\Release\net9.0-windows10.0.19041.0\protected\Vanara.Core.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "G:\Programok\Speaking_Clock\Speaking_Clock\bin\x64\Release\net9.0-windows10.0.19041.0\protected\Telerik.Licensing.Runtime.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "G:\Programok\Speaking_Clock\Speaking_Clock\bin\x64\Release\net9.0-windows10.0.19041.0\protected\Vanara.PInvoke.CoreAudio.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "G:\Programok\Speaking_Clock\Speaking_Clock\bin\x64\Release\net9.0-windows10.0.19041.0\protected\Vanara.PInvoke.DwmApi.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "G:\Programok\Speaking_Clock\Speaking_Clock\bin\x64\Release\net9.0-windows10.0.19041.0\protected\Vanara.PInvoke.Gdi32.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "G:\Programok\Speaking_Clock\Speaking_Clock\bin\x64\Release\net9.0-windows10.0.19041.0\protected\Vanara.PInvoke.Kernel32.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "G:\Programok\Speaking_Clock\Speaking_Clock\bin\x64\Release\net9.0-windows10.0.19041.0\protected\Vanara.PInvoke.Ole.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "G:\Programok\Speaking_Clock\Speaking_Clock\bin\x64\Release\net9.0-windows10.0.19041.0\protected\Vanara.PInvoke.Shared.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "G:\Programok\Speaking_Clock\Speaking_Clock\bin\x64\Release\net9.0-windows10.0.19041.0\protected\Vanara.PInvoke.Shell32.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "G:\Programok\Speaking_Clock\Speaking_Clock\bin\x64\Release\net9.0-windows10.0.19041.0\protected\Vanara.PInvoke.User32.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "G:\Programok\Speaking_Clock\Speaking_Clock\bin\x64\Release\net9.0-windows10.0.19041.0\protected\Vortice.Direct2D1.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "G:\Programok\Speaking_Clock\Speaking_Clock\bin\x64\Release\net9.0-windows10.0.19041.0\protected\Vortice.DirectX.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "G:\Programok\Speaking_Clock\Speaking_Clock\bin\x64\Release\net9.0-windows10.0.19041.0\protected\Vortice.Mathematics.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "G:\Programok\Speaking_Clock\Speaking_Clock\bin\x64\Release\net9.0-windows10.0.19041.0\protected\Vortice.WinForms.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "G:\Programok\Speaking_Clock\Speaking_Clock\bin\x64\Release\net9.0-windows10.0.19041.0\protected\Vosk.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "G:\Programok\Speaking_Clock\Speaking_Clock\bin\x64\Release\net9.0-windows10.0.19041.0\protected\WinRT.Runtime.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "G:\Programok\Speaking_Clock\Speaking_Clock\bin\x64\Release\net9.0-windows10.0.19041.0\protected\ZstdSharp.dll"; DestDir: "{app}"; Flags: ignoreversion
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{autoprograms}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Run]
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent runascurrentuser;

[Code]
function InitializeSetup: Boolean;
begin
Dependency_AddDotNet90Desktop;
  Result := True;
end;

procedure InitializeWizard;
var
  ResultCode: Integer;
begin
  if not (IsWin64 and (GetWindowsVersion >= $0A000000)) then
  begin
    MsgBox('Ennek az alkalmazásnak Windows 10 (64-bit) vagy újabbra rendszerre van szüksége. A telepítés megszakadt.', mbError, MB_OK);
    Abort;
  end;
end;

const
  GitHubAPIURL = 'https://api.github.com/repos/abbodi1406/vcredist/releases/latest';
  VCRedistFileName = 'VisualCppRedist_AIO_x86_x64.exe';

function OnDownloadProgress(const Url, Filename: String; const Progress, ProgressMax: Int64): Boolean;
begin
  if ProgressMax <> 0 then
    Log(Format('Downloading %s: %d of %d bytes...', [Filename, Progress, ProgressMax]))
  else
    Log(Format('Downloading %s: %d bytes...', [Filename, Progress]));
  Result := True;
end;

function DownloadString(const URL: string): string;
var
  Http: Variant;
begin
  Result := '';
  try
    Http := CreateOleObject('MSXML2.XMLHTTP');
    Http.Open('GET', URL, False);
    Http.setRequestHeader('User-Agent', 'InnoSetup');
    Http.Send;
    if Http.Status = 200 then
      Result := Http.ResponseText;
  except
    Log('Failed to fetch URL: ' + URL);
  end;
end;

// Helper functions for simple substring search
function PosSimple(const SubStr, Str: string; StartPos: Integer): Integer;
var
  I: Integer;
begin
  Result := 0;
  for I := StartPos to Length(Str) - Length(SubStr) + 1 do
  begin
    if Copy(Str, I, Length(SubStr)) = SubStr then
    begin
      Result := I;
      Exit;
    end;
  end;
end;

function RPosSimple(const SubStr, Str: string; BeforeIndex: Integer): Integer;
var
  I: Integer;
begin
  Result := 0;
  if BeforeIndex > Length(Str) then
    BeforeIndex := Length(Str);
  for I := 1 to BeforeIndex - Length(SubStr) + 1 do
  begin
    if Copy(Str, I, Length(SubStr)) = SubStr then
      Result := I;
  end;
end;

// Extract the asset URL from GitHub JSON
function ExtractDownloadURL(const Json: string): string;
var
  SearchStr: string;
  P1, P2: Integer;
begin
  Result := '';
  SearchStr := '"' + VCRedistFileName + '"';
  P1 := PosSimple(SearchStr, Json, 1);
  if P1 > 0 then
  begin
    P1 := RPosSimple('"browser_download_url":"', Json, P1);
    if P1 > 0 then
    begin
      P1 := P1 + Length('"browser_download_url":"');
      P2 := PosSimple('"', Json, P1);
      if P2 > P1 then
        Result := Copy(Json, P1, P2 - P1);
    end;
  end;
end;

// Main VC++ install routine
procedure InstallVCRedist;
var
  Json, DownloadURL, TempFile: string;
  ResultCode: Integer;
begin
  Log('Checking for latest VC++ release...');
  Json := DownloadString(GitHubAPIURL);
  if Json = '' then
  begin
    MsgBox('Nem sikerült elérni a GitHub API-t. Kézi Visual C++ Redistributable telepítés szükséges.', mbError, MB_OK);
    Exit;
  end;

  DownloadURL := ExtractDownloadURL(Json);
  if DownloadURL = '' then
  begin
    MsgBox('Nem található a VisualCppRedist_AIO_x86_x64.exe a GitHub kiadásban.', mbError, MB_OK);
    Exit;
  end;

  Log('Downloading from: ' + DownloadURL);
  try
    DownloadTemporaryFile(DownloadURL, VCRedistFileName, '', @OnDownloadProgress);
    TempFile := ExpandConstant('{tmp}\') + VCRedistFileName;
  except
    MsgBox('Nem sikerült letölteni a VisualCppRedist_AIO_x86_x64.exe fájlt.' + #13#10 + GetExceptionMessage, mbError, MB_OK);
    Exit;
  end;

  if FileExists(TempFile) then
  begin
    Log('Running VisualCppRedist installer...');
    Exec(TempFile, '/y', '', SW_SHOW, ewWaitUntilTerminated, ResultCode);
  end
  else
    MsgBox('A letöltött fájl nem található: ' + TempFile, mbError, MB_OK);
end;

// Run VC++ setup if component selected
procedure CurStepChanged(CurStep: TSetupStep);
begin
  if (CurStep = ssInstall) and WizardIsComponentSelected('vcredist') then
    InstallVCRedist;
end;